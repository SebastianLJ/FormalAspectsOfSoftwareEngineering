scheme  ColouringBasics =
class
  type 
    Piece = Text,
    Relation = (Piece >< Piece)-set,
    Colour = Piece-set,
    Colouring = Colour-set
    
  value /* auxiliary functions */ 

    isRelation : Relation -> Bool
    isRelation(r) is 
      (all (cn1, cn2) : Piece >< Piece :- (cn1, cn2) isin r => (cn1 ~= cn2 /\ cn1 ~= "" /\ cn2 ~= "")),

    areNb : Piece >< Piece >< Relation -> Bool
    areNb(cn1, cn2, r) is 
      (cn1, cn2) isin r \/ (cn2, cn1) isin r,

    isCorrectColouring : Colouring >< Relation -> Bool
    isCorrectColouring(cols, r) is
      /* All Pieces in a Colour of cols must also appear in r */
      (all col : Colour :- col isin cols =>
        (all cn : Piece :- cn isin col =>
          (exists (cn1,cn2) : Piece >< Piece :- (cn1,cn2) isin r /\
            (cn = cn1 \/ cn = cn2)
          )
        )
      )
      /\
      /* All Pieces in a Colour must only appear in exactly one Colour of cols */
      (all col1 : Colour :- col1 isin cols =>
        (all col2 : Colour :- col2 isin cols =>
          col1 ~= col2 => col1 inter col2 = {}
        ) 
      )
      /\
      /* All pieces in r must also appear in a Colour of cols */
      (all (cn1, cn2) : Piece >< Piece :- (cn1, cn2) isin r =>
        (exists col1 : Colour :- col1 isin cols /\
          (exists col2 : Colour :- col2 isin cols /\
            cn1 isin col1 /\ cn2 isin col2
          )
        )
      )
      /\
      /* All Pieces in the same Colour of cols must not be neighbours */
      (all col : Colour :- col isin cols =>
        (all cn1 : Piece :- cn1 isin col =>
          (all cn2 : Piece :- cn2 isin col =>
            areNb(cn1,cn2,r) = false
          )
        )
      )

 end
